apply plugin: 'checkstyle'
apply plugin: 'com.android.application'
apply plugin: "com.github.spotbugs"

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "open.furaffinity.client"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 37
        versionName "2022.06.04"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/*.kotlin_module']
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

checkstyle {
    configFile file("${checkstyleConfigRoot}/checkstyle.xml")
    configProperties = [
            'checkstyle.cache.file': "${checkstyleConfigRoot}/cache",
            'checkstyle.suppressions.file': "${checkstyleConfigRoot}/suppressions.xml",
            'checkstyle.header.file': "${checkstyleConfigRoot}/java.header",
            'checkstyle.regexp.header.file': "${checkstyleConfigRoot}/java_regexp.header",
            'checkstyle.suppressions-xpath.file': "${checkstyleConfigRoot}/suppressions-xpath.xml",
            'checkstyle.importcontrol.file': "${checkstyleConfigRoot}/import-control.xml",
            'checkstyle.importcontroltest.file': "${checkstyleConfigRoot}/import-control-test.xml"
    ]
    description 'Check code standard'
    group 'verification'
    ignoreFailures = true//Will ignore these for the moment so that we can build and fix
    showViolations = false//Hiding this for now as there are to many.
    toolVersion '10.3'
}

task checkstyle(type: Checkstyle) {
    classpath = files()
    reports {
        xml.enabled false
        html.enabled true
    }
    source 'src/main/java'
}

spotbugs {
    description 'Check code standard'
    effort = 'max'
    group 'verification'
    ignoreFailures = true//Will ignore these for the moment so that we can build and fix
    reportsDir = file("$buildDir/reports/spotbugs")
    reportLevel = 'low'
    showProgress = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

project.afterEvaluate {
    preBuild.dependsOn 'checkstyle'
    assembleDebug.dependsOn 'spotbugsDebug'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'app.cash.quickjs:quickjs-android:0.9.2'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.work:work-runtime:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation("com.squareup.okhttp3:okhttp:4.9.0")
    implementation 'org.jsoup:jsoup:1.13.1'

    implementation group: 'org.json', name: 'json', version: '20210307'

    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha06'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    implementation 'com.github.barteksc:android-pdf-viewer:3.2.0-beta.1'
    implementation 'com.github.joniles:rtfparserkit:1.16.0'
    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13.2'

}
