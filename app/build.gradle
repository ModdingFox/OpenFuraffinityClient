plugins {
    id 'checkstyle'
    id 'com.android.application'
    id 'com.github.spotbugs'
}

android {
    namespace 'open.furaffinity.client'
    compileSdk 32

    defaultConfig {
        applicationId "open.furaffinity.client"
        minSdk 29
        targetSdk 32
        versionCode 38
        versionName "20221122T074760(v38 Dev Build)"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

checkstyle {
    configFile file("${checkstyleConfigRoot}/checkstyle.xml")
    configProperties = [
            'checkstyle.cache.file': "${checkstyleConfigRoot}/cache",
            'checkstyle.suppressions.file': "${checkstyleConfigRoot}/suppressions.xml",
            'checkstyle.header.file': "${checkstyleConfigRoot}/java.header",
            'checkstyle.regexp.header.file': "${checkstyleConfigRoot}/java_regexp.header",
            'checkstyle.suppressions-xpath.file': "${checkstyleConfigRoot}/suppressions-xpath.xml",
            'checkstyle.importcontrol.file': "${checkstyleConfigRoot}/import-control.xml",
            'checkstyle.importcontroltest.file': "${checkstyleConfigRoot}/import-control-test.xml"
    ]
    description 'Check code standard'
    group 'verification'
    ignoreFailures = false
    showViolations = true
    toolVersion '10.3'
}

task checkstyle(type: Checkstyle) {
    classpath = files()
    reports {
        xml.enabled false
        html.enabled true
    }
    source 'src/main/java'
}

spotbugs {
    description 'Check code standard'
    effort = 'max'
    excludeFilter = file("${spotbugsConfigRoot}/exclude.xml")
    group 'verification'
    ignoreFailures = false
    reportsDir = file("$buildDir/reports/spotbugs")
    reportLevel = 'low'
    showProgress = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

project.afterEvaluate {
    preBuild.dependsOn 'checkstyle'
    assembleDebug.dependsOn 'spotbugsDebug'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'org.jsoup:jsoup:1.15.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
}